#class gps open;

#alias {INVOKE} {
	#var gps_status 0;
	#script {file_name} $cmd;
	#var file_name ${file_name}[1];
	#if {"${file_name}" != "0"} {
        	#read ${file_name};
        	#system {rm ${file_name}};
    	} {
		#var gps_status 1;
		#showme {查询失败，可能需要好好检查一下位置};
	};
};

#alias {fly} {
	#var dst_name %1;
	#if { "$dst_name" == "" } {
		#showme 请输入目标地址：（例如） 少林寺方丈楼;
	} {
		gps.fly $dst_name;
	};
};

#alias {gps.fly} {		
	gps.check_dst %1;
	#if { "$gps_status" == "0" } {
		#if { "$dst_num" != "1" } {#showme 目标地址不唯一，选择第一个符合条件的房间执行;};
		gps.check_src {gps_check_room_done};
	} {
		#showme 目标地址无法定位;
	}; 

	#alias {gps_check_room_done} {
		#if { "$gps_status" == "0" } {
			#if { $src_num > 1 } {
				#showme 源地址不唯一，选取第一个合适的房间执行;
			}{
				#var cmd {php ./bin/get_path.php "${src_roomno}" "${dst_roomno}" "${user.id}" };
 				#nop {#showme $cmd;};
				INVOKE;
				#showme 飞行路线图：$fly_path;
				#showme 飞行距离： $fly_num;
				gps.walk;
			};
		} {
            	#showme 源地址无法定位;
		};
	};
};


#alias {gps.walk} {
    #var step 1;
	#var tmp_step 1;
	#var new_path {};
	gps.getpath;
};

#alias {gps.run} {gps.getpath};
#alias {gps.getpath}{
	#if { $step >  @numitems{fly_path} } {
		#showme 设置完成;
		#showme $new_path;
		gps.walkpath {gps_walk_done};
	} {
		#if { "$tmp_step" == "25"  } {
			gps.walkpath {gps.getpath};		
		} {
			#var tmp_dir @item{fly_path;$step};
			#math step {$step+1};
			#math tmp_step {$tmp_step+1};
			#if { "@convertAlias{$tmp_dir}" == "0" } {
				#format {new_path} {%s%s|} $new_path @convertDir{$tmp_dir};
				gps.getpath;
			} {
				gps.walkpath {$tmp_dir};
			}
		};
	};
};


#alias {gps.walkpath} {
	#alias tmp_walk #cr;
    	#alias tmp_walk %0;
	#var tmp_step 1;
	#if { "$new_path" == "" } {
		tmp_walk;
	} {
		#delay {2} {
			set walk_path $new_path;
			set walk_speed -1;
			walk path;
			#var new_path {};
		};
	};

	#alias {gps_walk_done} {
		#showme 抵达目标地点：$dst_name;
	};

	#action {你的自建路径行走到达了终点。} {
		tmp_walk;
	};
};

#function {convertAlias} {
	#var str %1;
	#list special_dir_list {create} {cross;crush;wieldweapon;};
	#regex {$str} {%* %*} {#var str &1;};
	#list {special_dir_list} find $str result;
};

#function {numitems} {#list %1 size result};
#function {ismember} {#list %2 find %1 result};
#function {item} {#list %1 get %2 result};
#function {eval} {#math result {%1}};
#function {convertDir} {
	#local dir %1;
	#var {pathdir} {
		{e}{east}{n}{north}{w}{west}{s}{south} 
		{se}{southeast}{sw}{southwest}{ne}{northeast}{nw}{northwest}
		{su}{southup}{sd}{southdown}{nu}{northup}{nd}{northdown}
		{eu}{eastup}{ed}{eastdown}{wu}{westup}{wd}{westdown}
	};
	#if { "$pathdir[$dir]" == "" } {
		#return {do_$dir};
	} {
		#return $pathdir[$dir];
	} 
};

#gag clear: done;
#alias {clear} {
    kill %0;
    #class clear.inner open;
    #action {^这里没有这个人} {
        #unaction {^这里没有这个人};
        #class clear.inner kill;
        raw;
        #showme clear: done;
    } {1};
    #ticker {kill} {
        kill %0;
    } {3};
    #class clear.inner close;
};

#alias {on_cleared} {
    #alias tmp_on_cleared %0;
    #action {clear: done} {
        #unaction {clear: done};
        #delay {3} {
            tmp_on_cleared;
        };
    };
};

#alias {wieldweapon} {wield $my_weapon;gps.run;};

#alias {crush} {
	#var target %0;
	#var style %1;
	#switch {"$style"}{
		#case {"wait"} {gps.wait %2};
		#case {"delay"} {#delay {%2} {gps.getpath}};
		#default {gps.clear $target};
	}
};

#alias {gps.wait} {
    #var next_action %1;
    #class gps.action.inner open;
    #action {^{你的动作还没有完成，不能移动。|你走着走着就陷进了一处沼泽当中，艰难地从沼泽中拔出来。|你小心翼翼往前挪动，遇到艰险难行处，只好放慢脚步。|你还在山中跋涉，一时半会恐怕走不出这藏边群山！|青海湖畔美不胜收，你不由停下脚步，欣赏起了风景。|你还在山中跋涉，一时半会恐怕走不出这西南地绵绵群山！|你还在山中跋涉，一时半会恐怕走不出这六盘山！}} {
        #delay {again} {
            ${next_action};
        } {1};
        #delay {gps_delay} {
            #class gps.action.inner kill;
            gps.run;
        } {2};
    } {1};
    #delay {gps_delay} {
        #class gps.action.inner kill;
        gps.run;
    } {1};
    #class gps.action.inner close;

    ${next_action};
};


#alias {gps.clear} {
    clear %0;
    on_cleared {
        gps.getpath;
    };
};

#alias {cross} {
	#var target %2;
      #var style %1;
      #switch {"$style"} {
            #case {"che"} {
			#switch {"$target"} {
				#case {"hh"} {cross_river 过河};
				#case {"cj"} {cross_river 过江};
				#case {"boat"} {enter_boat};
				#default {zuoche $target};
			};
		};
            #case {"delay"} {#delay {%2} {gps.getpath}};
      }
};

#alias {enter_boat} {
    	enter boat;
    	#class gps.action.inner open;
    	#action {小舟终于划到近岸，你从船上走了出来。} {
        	#unaction {小舟终于划到近岸，你从船上走了出来。};
        	gps.run;
    	};
    	#action {绿衣少女将小船系在树枝之上，你跨上岸去。} {
        	#unaction {绿衣少女将小船系在树枝之上，你跨上岸去。};
        	gps.run;
    	};
    	#action {你沿着踏板走了上去} {
        	#unaction {你沿着踏板走了上去};
        	gps.run;
    	};
    	#action {你朝船夫挥了挥手便跨上岸去} {
        	#unaction {你朝船夫挥了挥手便跨上岸去};
        	gps.run;
	};
	#class gps.action.inner close;
};


#alias {cross_river} {
	#var tmp_word %1;
	ask shao gong about $tmp_word;
	#action {正等着你呢，上来吧。} {#class tmp kill;enter};
	#action {艄公将一块踏脚板搭上堤岸，} {#class tmp kill;enter};
	#action {^{%*接过你递给的船资|你的车船通账上还剩%S，这一趟的船资%S。|你吸了口气，一声“船家”，声音中正平和地远远传了出去。|你使出吃奶的力气喊了一声：“船家”}} {
		#class tmp open;
            #action {^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐……”} {#class tmp kill;#delay {3} {yell boat}};
            #class tmp close;
      };
	#var in_boat 0;
      #action {^江心驶去。} {
		#var in_boat 1;
      };
      #action {^艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸。} {
            #if {${in_boat} == 1} {
                   #var in_boat 0;
                   out;gps.run;
		};
	};
};


#alias {zuoche} {
    #ticker {gps_ticker} {
        hire;
        qu %1;
    } {2};
    #class gps.zuoche.inner open;
    #action {你登上了一辆马车} {
        #unticker {gps_ticker};
    };
    #action {大车停稳了下来} {
        #unaction {大车停稳了下来};
        #delay {zuoche} {xia;gps.run;} {3};
    };
    #class gps.zuoche.inner close;
};


#alias {gps.check_src} {
    	#alias tmp_check_room #cr;
    	#alias tmp_check_room %1;
    	#alias {get_pos_done} {
		#if { "${pos.area}" == "大理城" } {#var pos.area {大理城中};};
		#if { "${pos.area}" == "荆州" } {#var pos.area {襄阳};};
		#if { "${pos.area}" == "建康府南" } {#var pos.area {建康府南城};};
		#if { "${pos.area}" == "建康府北" } {#var pos.area {建康府北城};};
		#var cmd {php ./bin/get_room.php "${pos.area}${pos.room}" "${user.id}" "${pos.exits}"};
        	INVOKE;
        	#showme $result;
		#var src_num  $num;
		#var src_roomno  $roomno;
		tmp_check_room;
    	};
    	get_area {get_pos {get_pos_done;};};
};


#alias {gps.check_dst} {
	#var dst_room %1;
	#replace {dst_room} {荆州的} {襄阳};
	#var cmd {php ./bin/get_room.php "$dst_room" "${user.id}"};
	INVOKE;
      #showme $result;
	#var dst_num $num;
	#var dst_roomno  $roomno;
};


#alias {get_pos} {
	#alias tmp_get_pos #cr;
    	#alias tmp_get_pos %1;
    	#var record_state 0;
    	#var current_room {};
    	#var description {};
    	#class record.inner open;
    	#action {^%* - $} {
        	#var record_state 1;
        	#var current_room %%1;
        	#if {"${current_room}" == "海中"} {
            	get_exits_done;
        	};
        	#elseif {"${current_room}" == "马车"} {
            	get_exits_done;
            	xia;
        	};
        	#elseif {"${current_room}" == "茅厕"} {
            	get_exits_done;
        	};
    	} {1};

    	#action {你可以看看(look):} {
        	#delay {look_delay} {
            	get_exits_done;
        	} {1};
    	} {6};

    	#action {    「%*」: %*} {
       	 #delay {look_delay} {
            	#var exits_list  {};
            	get_exits_done;
       	 } {1};
   	} {6};
   	#action {这里没有任何明显的出路。} {
      	  #var exits_list  {};
       	  get_exits_done;
    	} {7};

  	#action {这里唯一的{出口|方向}有 %w。} {
        	#var exits_list  {%%1;};
        	get_exits_done;
    	} {7};

    	#action {这里明显的{出口|方向}有 %w 和 %w。} {
        	#var exits_list {%%2;%%3;};
        	get_exits_done;
    	} {7};

    	#action {这里明显的{出口|方向}有 %w、%w 和 %w。} {
        	#var exits_list {%%2;%%3;%%4;};
        	get_exits_done;
    	} {7};

    	#action {这里明显的{出口|方向}有 %w、%w、%w 和 %w。} {
        	#var exits_list {%%2;%%3;%%4;%%5;};
        	get_exits_done;
    	} {7};

    	#action {这里明显的{出口|方向}有 %w、%w、%w、%w 和 %w。} {
        	#var exits_list {%%2;%%3;%%4;%%5;%%6;};
        	get_exits_done;
    	} {7};

    	#action {这里明显的{出口|方向}有 %w、%w、%w、%w、%w 和 %w。} {
        	#var exits_list {%%2;%%3;%%4;%%5;%%6;%%7;};
        	get_exits_done;
    	} {7};

    	#action {这里明显的{出口|方向}有 %w、%w、%w、%w、%w、%w 和 %w。} {
        	#var exits_list {%%2;%%3;%%4;%%5;%%6;%%7;%%8;};
        	get_exits_done;
    	} {7};

    	#nop max 7 exits;
    	#action {这里明显的{出口|方向}有 %w、%w、%w、%w、%w、%w、%w} {
        	#var exits_list {%%2;%%3;%%4;%%5;%%6;%%7;%%8;};
        	get_exits_done;
    	} {7};

    	#alias {get_exits_done} {
        	#var pos.room ${current_room};
        	#var pos.exits ${exits_list};
        	#class record.inner kill;
        	#undelay {look_delay};
        	tmp_get_pos;
		#showme $pos;
    	};

    	#class record.inner close;
    	#send {l};
};


#alias {get_area} {
	#alias tmp_get_area #cr;
    	#alias tmp_get_area %1;
    	#var pos.area {};
    	#class record.inner open;
    	#action {这里暂时没有地图。} {
        	#class record.inner kill;
        	#var pos.area {};
        	tmp_get_area;
    	};
    	#action {【%*地图】} {
        	#class record.inner kill;
        	#var pos.area %%1;
        	tmp_get_area;
    	};
    	#action {【%*地图－%*】} {
        	#class record.inner kill;
        	#var pos.area %%1;
        	tmp_get_area;
    	};
    	#class record.inner close;
    	localmaps;
    	q;
};


#class gps close;
